#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;
use autodie qw(open close);

use Carp qw(croak);
use IPC::Run3;

our $VERSION = '0.500';

my $PERLBREW_ROOT = $ENV{PERLBREW_ROOT} || "$ENV{HOME}/perl5/perlbrew";

my $as = shift or croak "Usage: $0 <perl-version>";
my @args = @ARGV;

# trailing "t" means do threads
my @threads = ($as =~ /t$/) ? (qw/-D usethreads/) : ();

$as =~ s/^5[.]//;
my $perl = "5.$as";
$perl =~ s/t$//; # strip trailing "t" if any
my $lib = "$as\@std";

my @problem_modules = qw(
    Bundle::CPAN
    JSON::XS
);

my @to_install = qw(
    Task::BeLike::RJRAY
);

# Determine number of jobs to use in the make invocation:
my $ncpu = 1;
OS_CHECK: {
    # "Borrowed" from MCE::Util::get_ncpu():
    local $_ = $^O;

    /linux/i && do {
        my $count;
        if (open my $PROC, '<', '/proc/stat') {
            $count = grep { /^cpu\d/ } <$PROC>;
            close $PROC;
        }
        if ($count) {
            $ncpu = $count;
        }
        last OS_CHECK;
    };

    /bsd|darwin|dragonfly/i && do {
        my @output;

        run3 [ qw(sysctl -n hw.ncpu) ], \undef, \@output, \undef;
        if ($output[0] && $output[0] =~ /(\d+)/) {
            $ncpu = $1;
        }
        last OS_CHECK;
    };

    warn "Unable to determine number of CPU cores for make.\n";
}

# Add one to $ncpu for make's controller process:
$ncpu++;

# install perl and lock it down
system qw/perlbrew install -j/, $ncpu,  qw/--as/, $as, $perl, @threads, @args;
system qw/chmod -R a-w/, "$PERLBREW_ROOT/perls/$as";

# give us a local::lib for installing things
system qw/perlbrew lib create/, $lib;

# let's avoid any pod tests when we try to install stuff
system qw/perlbrew exec --with/, $lib, qw/cpanm TAP::Harness::Restricted/;
local $ENV{HARNESS_SUBCLASS} = 'TAP::Harness::Restricted';

# some things need forcing
system qw/perlbrew exec --with/, $lib, qw/cpanm -f/, @problem_modules;

# now install the rest
if (@to_install) {
    system qw/perlbrew exec --with/, $lib, qw/cpanm/, @to_install;

    # repeat to catch any circularity problems
    system qw/perlbrew exec --with/, $lib, qw/cpanm/, @to_install;
}

exit;
